# BLOCK 6 : GREEN HOUSE GASES EMISSIONS

include lists

# equation 6.1
EMS_CH[s] = EMS_21[s], s in %list_sec
EMS_PT[s] = EMS_22[s], s in %list_sec
EMS_GZ[s] = EMS_24[s], s in %list_sec

d(log(EMS_21[s])) = d(log(E_21[s])) if EMS_21[s] <> 0, s in %list_sec
d(log(EMS_22[s])) = d(log(E_22[s]) + log(PhiY_22_2201)) if EMS_22[s] <> 0, s in %list_sec
d(log(EMS_24[s])) = d(log(E_24[s]) + log(PhiY_24_2401)) if EMS_24[s] <> 0, s in %list_sec

# equation 6.2
EMS[s] = sum(EMS_|V|[s] if EMS_|V|[s] <> 0, V in GZ CH PT), s in %list_sec

  # PROBLEME! Agreger les secteurs energ√©tiques en appellant la subroutine aggregate_energy
IC_21=EMS_CH/(QD_21+M_21-X_21)
IC_22=(EMS_PT/(QD_22+M_22-X_22))*PhiY_22_2201
IC_24=(EMS_GZ/(QD_24+M_24-X_24))*PhiY_24_2401

# ***CO2 SECTORS EMISSIONS***
# equation 6.3
EMS_S = sum(EMS[s] if EMS[s] <> 0, s in %list_sec)

# DECARBONATION

# equation 6.4
d(log(EMS_DC[c])) = d(log(MAT[c])) if EMS_DC[c] <> 0, c in %list_com

EMS_DC = sum(EMS_DC[c] if EMS_DC[c] <> 0, c in %list_com)

# EMISSIONS by energy source
# equation 6.5
EMS_|V| = sum(EMS_|V|[s] if EMS_|V|[s] <> 0, s in %list_sec), V in GZ CH PT


# ***CO2 HOUSEHOLDS EMISSIONS HYBRID***

#  if %hybrid_household="yes" then #Load if "yes" in the main
#    Call BLOCK_GHG_Hybrid

# ***END CO2 HOUSEHOLDS EMISSIONS HYBRID***

#  else

# ***CO2 HOUSEHOLDS EMISSIONS***

# equation 6.6
d(log(EMS_HH[ce2, h])) = d(log(EXP[ce2, h])), ce2 in %list_com_E_CO2, h in %list_household
EMS_HH[ce2] = sum (EMS_HH[ce2, h], h in %list_household), ce2 in %list_com_E_CO2
EMS_HH = sum (EMS_HH[ce2], ce2 in %list_com_E_CO2)

EMS_TOT=EMS_S+EMS_DC+EMS_HH

# ***END CO2 HOUSEHOLDS EMISSIONS***

#  endif

# ***ENERGY PRODUCTION IN MTEP***

# equation 6.7
d(log(Q_Mtep_H[ce]))=d(log(EXP[ce])), ce in %list_com_E

# equation 6.8
d(phiY_22[se]) = d(phi_Mtep[se]), se in %list_subsec_22
d(phiY_23[se]) = d(phi_Mtep[se]), se in %list_subsec_23
d(phiY_24[se]) = d(phi_Mtep[se]), se in %list_subsec_24

# equation 6.9
Q_Mtep_22 = sum(Q_Mtep[se] if Q_Mtep[se] <> 0, se in %list_subsec_22)
Q_Mtep_23 = sum(Q_Mtep[se] if Q_Mtep[se] <> 0, se in %list_subsec_23)
Q_Mtep_24 = sum(Q_Mtep[se] if Q_Mtep[se] <> 0, se in %list_subsec_24)

Q_Mtep = sum(Q_Mtep[se] if Q_Mtep[se] <> 0, se in %list_sec_E)

# Energy Production in primary energy
 # equation 6.10
 Q_Mtep_EP[se] = Q_Mtep[se] * fac_conv_EP[se], se in %list_sec_E

Q_Mtep_EP_22 = sum(Q_Mtep_EP[se] if Q_Mtep_EP[se] <> 0, se in %list_subsec_22)
Q_Mtep_EP_23 = sum(Q_Mtep_EP[se] if Q_Mtep_EP[se] <> 0, se in %list_subsec_23)
Q_Mtep_EP_24 = sum(Q_Mtep_EP[se] if Q_Mtep_EP[se] <> 0, se in %list_subsec_24)

Q_Mtep_EP = sum(Q_Mtep_EP[se] if Q_Mtep_EP[se] <> 0, se in %list_sec_E)

# Energy consumption from the activity sectors
# equation 6.11
E_SEC|O|[ce] = sum(E|O|[ce, cm], cm in %list_com_MAT), O in D M, ce in %list_com_E

# equation 6.12
E_SEC[ce] = sum(E_SEC|O|[ce], O in D M), ce in %list_com_E

 # equation 6.13
d(log(Q_Mtep_SEC[ce])) = d(log(E_SEC[ce])) if E_SEC[ce] <> 0, ce in %list_com_E

Q_Mtep_SEC = sum(Q_Mtep_SEC[ce] if Q_Mtep_SEC[ce] <> 0, ce in %list_com_E)

# equation 6.14
E_ESEC|O|[ce] = sum(E|O|[ce, se], se in %list_sec_E), O in D M, ce in %list_com_E
E_ESEC[ce] = sum(E_ESEC|O|[ce], O in D M), ce in %list_com_E

d(log(Q_Mtep_ESEC[ce])) = d(log(E_ESEC[ce])) if E_ESEC[ce] <> 0, ce in %list_com_E
Q_Mtep_ESEC = sum(Q_Mtep_ESEC[ce] if Q_Mtep_ESEC[ce] <> 0, ce in %list_com_E)

# Energy final consumption
# equation 6.15
Q_Mtep_EF[ce] = Q_Mtep[ce]-Q_Mtep_ESEC[ce], ce in %list_com_E
Q_Mtep_EF = sum(Q_Mtep_EF[ce] if Q_Mtep_EF[ce] <> 0, ce in %list_com_E)

# Energy production by energy sector
#if %hybrid_household="yes" then

# equation 6.16
Q_Mtep_21=Q_Mtep_SEC_21+Q_Mtep_H_21+Q_Mtep_ESEC_21

Q_Mtep[se] = phi_Mtep[se] * (Q_Mtep_H_22 + Q_Mtep_SEC_22 + Q_Mtep_ESEC_22), se in %list_subsec_22
Q_Mtep[se] = phi_Mtep[se] * (Q_Mtep_H_23 + Q_Mtep_SEC_23 + Q_Mtep_ESEC_23), se in %list_subsec_23
Q_Mtep[se] = phi_Mtep[se] * (Q_Mtep_H_24 + Q_Mtep_SEC_24 + Q_Mtep_ESEC_24), se in %list_subsec_24

# ***CARBON TAX***
# equation 6.17
TCO_VAL[ce2, s] = Ttco * EMS[ce2, s] * (1 - EXO[ce2, s]), ce2 in %list_com_E_CO2, s in %list_sec
TCO_VAL_sec[ce2] = sum(TCO_VAL[ce2, s], s in %list_sec), ce2 in %list_com_E_CO2
TCO_VAL[s] = sum(TCO_VAL[ce2, s], ce2 in %list_com_E_CO2), s in %list_sec
TCO_VAL = sum(TCO_VAL[s], s in %list_sec)

# equation 6.18
TCO_VAL|O|[ce, s] = TCO_VAL[ce, s] * E|O|[ce, s]/E[ce, s] if E[ce, s] <> 0, O in D M, ce in %list_com_E, s in %list_sec
TCO_VAL|O|_sec[ce2] =  sum(TCO_VAL|O|[ce2, s] , s in %list_sec), O in D M, ce2 in %list_com_E_CO2

# equation 6.19
TCO_HH_VAL[ce2, h] = Ttco * EMS_HH[ce2, h], ce2 in %list_com_E_CO2, h in %list_household
TCO_HH_VAL_com[ce2] = sum(TCO_HH_VAL[ce2, h], h in %list_household), ce2 in %list_com_E_CO2
TCO_HH_VAL[h] = sum(TCO_HH_VAL[ce2, h], ce2 in %list_com_E_CO2), h in %list_household
TCO_HH_VAL = sum(TCO_HH_VAL[h],h in %list_household)

# equation 6.20
Rec_TCO_VAL=Ttco*(EMS_HH+EMS_S-EMS_21_10-EMS_22_09-EMS_24_08-EMS_24_07)
Rec_TCO=REC_TCO_VAL/PGDP
CO_VAL=1000000*(REC_TCO/(EMS_TOT*PGDP))
#series CO_VAL=1000000*(REC_TCO+CIDD+Bonus+Bonus_elec+CSPE)/(EMS_TOT*PGDP))# equation telle qu#elle est dans la version V1#

CO2_price_signal= (ENERT_21+OTHT_21+ENERT_22+ENERT_24+Rec_TCO)*1000000/EMS_TOT
EN_price_signal = (ENERT_21 +OTHT_21+ENERT_22+OTHTD_23+ENERT_24+Rec_TCO)/Q_Mtep_EP

# ***ENDOGENOUS EMPLOYER SOCIAL CONTRIBUTION RATE WHEN CARBON TAX***
# equation 6.21
Rec_TCO_ETS = sum(Ttco * EMS[s], s in %list_sec \ 01 02 03 11 12 13 14 15 16 17 19 20)
WAGES_ETS = sum(W_S[s] * L_S[s], s in %list_sec \ 01 02 03 11 12 13 14 15 16 17 19 20)
Rec_TCO_NETS = sum(tTCO * EMS[s], s in %list_sec \ 04 05 06 07 08 09 10 18 21 2201 2202 2301 2302 2303 2304 2305 2306 2307 2308 2401 2402 2403 2404 2405 2406)

WAGES_NETS = W_S * L_S - WAGES_ETS
